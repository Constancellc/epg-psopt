clear
set defaultbasefrequency=60

! IEEE 4-bus test case   Y-Y Stepdown Balanced
! Based on script developed by Alan Dunn and Steve Sparling

new circuit.4BusYYbal basekV=12.47 phases=3 
! **** HAVE TO STIFFEN THE SOURCE UP A LITTLE; THE TEST CASE ASSUMES AN INFINITE BUS
~ mvasc3=200000 200000

set earthmodel=carson

! **** DEFINE WIRE DATA 
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 

! **** DEFINE LINE GEOMETRY; REDUCE OUT THE NEUTRAL
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 

! **** 12.47 KV LINE
new line.line1 geometry=4wire length=2000 units=ft bus1=sourcebus bus2=n2

! **** 3-PHASE STEP-DOWN TRANSFORMER 12.47/4.16 KV Y-Y
new transformer.t1 xhl=6
~ wdg=1 bus=n2 conn=wye kV=12.47 kVA=6000 %r=0.5 
~ wdg=2 bus=n3 conn=wye kV=4.16  kVA=6000 %r=0.5 

! **** 4.16 KV LINE
new line.line2 bus1=n3 bus2=n4 geometry=4wire length=2500 units=ft  

new capacitor.cap1 phases=1 bus1=n4.1 kV=2.4 kvar=1500
new capacitor.cap2 phases=1 bus1=n4.2 kV=2.4 kvar=1500
new capacitor.cap3 phases=1 bus1=n4.3 kV=2.4 kvar=1500

new load.load1a phases=1 bus1=n4.1 conn=wye kV=2.4 kW=1800 pf=0.9  model=1
~ vminpu=0.33, vmax=3.0    ! model will remain const p,q down to 0.75 pu voltage    
new load.load1b phases=1 bus1=n4.2 conn=wye kV=2.4 kW=1800 pf=0.9  model=1
~ vminpu=0.33, vmax=3.0    ! model will remain const p,q down to 0.75 pu voltage    
new load.load1c phases=1 bus1=n4.3 conn=wye kV=2.4 kW=1800 pf=0.9  model=1
~ vminpu=0.33, vmax=3.0    ! model will remain const p,q down to 0.75 pu voltage    


set voltagebases=[12.47, 4.16] 
calcvoltagebases     ! **** let DSS compute voltage bases
solve

/*
! Various reports ...
show voltages LN Nodes 
show currents resid=yes elements  ! this shows sum of phase currents
Show Powers kva Elements
*/